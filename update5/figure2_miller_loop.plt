# this figure was generated by putting this text into filename.plt and executing: gnuplot filename.plt

# note: last column is the color index

$data << EOD
v9\\_modelb\\_fractional 49073 3
v9\\_modelb\\_repriced 55741 3
v9\\_modelb\\_pushdup2 65513 3
v9\\_modelb 69149 3
v9\\_modela\\_fractional 71176 3
v7\\_modelb\\_fractional 76824 3
v7\\_modelb\\_repriced 86576 3
v9\\_modela\\_repriced 93443 3
v7\\_modela\\_fractional 98927 3
v9\\_modela\\_pushdup2 110082 3
v9\\_modela 113718 3
v7\\_modela\\_repriced 124278 3
v7\\_modelb\\_pushdup2 127183 2
v7\\_modelb 161654 1
v7\\_modela\\_pushdup2 171752 2
v7\\_modela 206223 1
EOD

set term png size 860,400 nocrop enhanced font "monospace,8"
set lmargin -1
set rmargin 10
set output "figure2_miller_loop.png"
set nokey
set boxwidth 0.5
set style fill solid
set xtics nomirror rotate by -45
set palette model RGB defined (1 "purple", 2 "red")
set yrange[0:250000]
set ylabel "gas"
set xtics 5

set key at graph 0.5, 0.8

# set colors, index matches the last data column
set linetype 1 lc rgb '#01915c' # dark green
set linetype 2 lc rgb 'orange'
set linetype 3 lc rgb 'brown'

plot "$data" using 0:2:3:xticlabels(1) with boxes title "various EVM repricings and changes" lc var,\
     "" using 0:($2+6000):(sprintf("%3i",$2)):(1) with labels notitle, \
     "" u 0 with histogram title "without changes to the EVM", \
     "" u 0 with histogram title "only repricing PUSH and DUP"

# note: $2 indicates column 2 is for y-coordinates
# note: after "using" in "plot" line we have four values separated by colons
#   0 means x value is index of row
#   2 means take y vals from col 2
#   3 means take color var from 3rd col
#   xticslabels(1) means take text labels from column 1
